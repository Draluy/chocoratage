plugins {
    id 'java'
    id "com.bmuschko.izpack" version "3.0"
}

group 'fr.raluy'
version '1.0-SNAPSHOT'

// very common version + freebsd doesn't support later than openjdk8: https://www.freebsd.org/java/
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'com.github.kwhat:jnativehook:2.2.1'
    compile 'com.ibm.icu:icu4j:68.2'
    compile 'org.slf4j:slf4j-api:1.7.29'
    compile 'org.slf4j:slf4j-simple:1.7.29'
    compile 'org.assertj:assertj-core:3.19.0'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'
    izpack 'org.codehaus.izpack:izpack-ant:5.1.3'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes "Main-Class": 'fr.raluy.chocoratage.Main'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    jar.archiveFileName = 'chocoratage.jar'
}

task copyLangSelImg(type: Copy) {
    from 'src/main/izpack/Choco_Wikipedia_Luc_Viatour.jpg'
    into "$buildDir/installer"
}

task copyScripts(type: Copy) {
    from 'src/main/scripts'
    into "$buildDir/scripts"
}

task copyDocs(type: Copy) {
    from 'README.md'
    into "$buildDir/docs"
}
// add LICENSE here if needed

izPackCreateInstaller.dependsOn copyLangSelImg
izPackCreateInstaller.dependsOn copyScripts
izPackCreateInstaller.dependsOn copyDocs

izpack {
    baseDir = file("$buildDir")
    //installFile = file('src/main/izpack/install.xml')
    //outputFile = file("$buildDir/distributions/chocoratage-${version}-installer.jar")
    compression = 'deflate'
    compressionLevel = 9
    /*
    appProperties = ['app.group': 'Chocoratage', 'app.name': 'chocoratage', 'app.title': 'Chocoratage',
                     'app.version': version, 'app.subpath': "Chocoratage-$version"]
     */
}

